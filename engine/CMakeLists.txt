cmake_minimum_required(VERSION 3.16.0)
project(BlackthornEngine VERSION 0.1.0 LANGUAGES CXX)

file(GLOB_RECURSE ENGINE_SOURCES
	"${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp"
)

file(GLOB_RECURSE ENGINE_HEADERS
	"${CMAKE_CURRENT_SOURCE_DIR}/include/*.h"
)

file(GLOB_RECURSE ENGINE_PRIVATE_HEADERS
	"${CMAKE_CURRENT_SOURCE_DIR}/private/*.h"
)

add_library(${PROJECT_NAME} SHARED
	${ENGINE_SOURCES}
	${ENGINE_HEADERS}
	${ENGINE_PRIVATE_HEADERS}
)

target_compile_definitions(${PROJECT_NAME}
	PRIVATE
		BLACKTHORN_EXPORTS

	PUBLIC
		$<$<CONFIG:Debug>:BLACKTHORN_DEBUG>
		$<$<CONFIG:Release>:BLACKTHORN_RELEASE>
)

target_compile_options(${PROJECT_NAME} PRIVATE
	-Wall
	-Wextra
	-Wpedantic
	-Wno-unused-parameter
	-Wshadow
	-Wduplicated-cond
)

target_include_directories(${PROJECT_NAME}
	PUBLIC
		$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
		$<INSTALL_INTERFACE:include>

	PRIVATE
		${CMAKE_CURRENT_SOURCE_DIR}/src
		${CMAKE_CURRENT_SOURCE_DIR}/private
)

target_link_libraries(${PROJECT_NAME}
	PUBLIC
		glm::glm
		SDL3::SDL3
    	SDL3_image::SDL3_image
		glad
		glfw
		
	PRIVATE
		OpenGL::GL
)

if(WIN32)
	# Copy DLL to output directory after build
	add_custom_command(TARGET ${PROJECT_NAME} POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different
		$<TARGET_FILE:${PROJECT_NAME}>
		${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
	)
	
	# Copy SDL3 DLL if it exists
	add_custom_command(TARGET ${PROJECT_NAME} POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different
		$<TARGET_FILE:SDL3::SDL3>
		${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
	)
	
	# Copy SDL3_image DLL if it exists
	add_custom_command(TARGET ${PROJECT_NAME} POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different
		$<TARGET_FILE:SDL3_image::SDL3_image>
		${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
	)
endif()

set_target_properties(${PROJECT_NAME} PROPERTIES
	OUTPUT_NAME ${PROJECT_NAME}
	DEBUG_POSTFIX "d"
	LINKER_LANGUAGE CXX
	RUNTIME_OUTPUT_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
	LIBRARY_OUTPUT_DIRECTORY ${CMAKE_LIBRARY_OUTPUT_DIRECTORY}
	ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}
)

if (CMAKE_BUILD_TYPE STREQUAL "Release" AND CMAKE_INTERPROCEDURAL_OPTIMIZATION)
	set_target_properties(${PROJECT_NAME} PROPERTIES
		INTERPROCEDURAL_OPTIMIZATION TRUE
	)
endif()